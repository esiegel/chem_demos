subprojects {

   repositories {
       mavenCentral()
   }

   // java plugin.
   apply plugin: "java"
   apply plugin: "eclipse"

   // custom source layout
   sourceSets {
       main {
           java {
               srcDir 'src'
           }
       }
   }

   // depend on jogl
   dependencies {
      compile group: 'org.jogamp.gluegen', name: 'gluegen',      version: '2.2.4'
      compile group: 'org.jogamp.gluegen', name: 'gluegen-rt',   version: '2.2.4'
      compile group: 'org.jogamp.jogl',    name: 'jogl-all',     version: '2.2.4'
      compile group: 'org.swinglabs',      name: 'swing-layout', version: '1.0.3'

      // Download the native libraries for you platform.  Currently OSX.
      compile group: 'org.jogamp.gluegen', name: 'gluegen-rt',   version: '2.2.4', classifier: 'natives-macosx-universal'
      compile group: 'org.jogamp.jogl',    name: 'jogl-all',     version: '2.2.4', classifier: 'natives-macosx-universal'
   }

   // Task to copy all of the jars.
   //
   // We need to copy the libraries to a single directory.  The jogl and gluegen
   // jars attempt to load native JNI libraries, those which are embedded within
   // the "natives" jars, by searching the same directory.  Sadly Gradle downloads
   // its dependencies into different directories.
   task copyLibs << {
      copy {
         from sourceSets.main.runtimeClasspath
         into 'build/libs'
         include '**/*.jar'
      }
   }

   // Copy libaries before creating the jar.
   jar.dependsOn(copyLibs)

   // Finds all jars in the libs directory and produces the manifests classpath
   ext.generateManifestClasspath = { ->
      def libsDir  = jar.destinationDir
      def paths = libsDir.listFiles()*.getAbsolutePath()

      // our classpath should only include the dependencies, not itself.
      paths.removeAll { it.endsWith(jar.archiveName) }

      return paths.join(' ')
   }

   // Running the code via the jars, since it has the correct jni libs paths.
   // Equivalent to running "java -jar PATH_TO_JAR".
   task run(dependsOn:jar) << {
        ant.java(jar: jar.archivePath, fork:true)
   }
}
